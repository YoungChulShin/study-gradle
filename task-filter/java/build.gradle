plugins {
    id 'java'
    id 'org.springframework.boot' version '3.0.3'
    id 'io.spring.dependency-management' version '1.1.0'
}

group = 'study.gradle'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
    useJUnitPlatform()
}

test {
    description 'Runs unit tests.'

    filter {
        excludeTestsMatching '*IntegrationTest'
    }
}

task testWithTag(type: Test) {
    description 'Runs unit tests using category'
    group 'verification'

    useJUnitPlatform {
        excludeTags("IntegrationTest")
    }
}

task integrationTest(type: Test) {
    description 'Runs integration tests.'
    group 'verification'

    filter {
        includeTestsMatching '*IntegrationTest'
    }
    useJUnitPlatform()
}

task integrationTestWithTag(type: Test) {
    description 'Runs integration tests.'
    group 'verification'

    useJUnitPlatform {
        includeTags("IntegrationTest")
    }
}


task allTest(type: Test) {
    description 'Runs all tests. (unit tests + integration tests).'
    group 'verification'

    useJUnitPlatform()
}
